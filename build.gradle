import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
}

ext {
    artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
    bintrayDryRun = false
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'maven-publish'

def dependencyVersions = [
        "com.squareup.okio:okio:2.1.0",
]

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force dependencyVersions
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    testRuntime 'org.slf4j:jul-to-slf4j:1.7.25'
    testRuntime 'ch.qos.logback:logback-classic:1.2.3'

    compile 'com.squareup.okio:okio:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.12.0'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.12.0'

    compile('com.kohlschutter.junixsocket:junixsocket-native-common:2.0.4') {
        exclude module: 'log4j'
    }
    compile('com.kohlschutter.junixsocket:junixsocket-common:2.0.4') {
        exclude module: 'log4j'
    }

    testRuntime "org.junit.platform:junit-platform-launcher:1.3.2"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.3.2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.3.2"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        dockerFilesocket(MavenPublication) {
            groupId 'de.gesellix'
            artifactId 'docker-filesocket'
            version artifactVersion
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER ?: project["bintray.user"]
    key = System.env.BINTRAY_API_KEY ?: project["bintray.key"]
    publications = ['dockerFilesocket']
    pkg {
        repo = 'docker-utils'
        name = 'docker-filesocket'
        desc = 'Unix Domain Sockets and Named Pipes for the JVM on Linux, macOS, and Windows'
        licenses = ['Apache-2.0']
        labels = ['docker', 'unix socket', 'linux', 'mac', 'named pipe', 'windows', 'engine api', 'remote api', 'client', 'java']
        version = [
                name: artifactVersion
        ]
        vcsUrl = 'https://github.com/docker-client/docker-filesocket.git'
    }
    dryRun = bintrayDryRun
}
bintrayUpload.dependsOn build

task updateWrapper(type: Wrapper) {
    gradleVersion = '5.0'
    distributionType = Wrapper.DistributionType.ALL
}
