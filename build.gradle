import java.text.SimpleDateFormat

buildscript {
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
  }
}

plugins {
  id 'com.gradle.build-scan' version '1.0'
}

ext {
  artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
  bintrayDryRun = false
}


apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

def dependencyVersions = [
    'org.bouncycastle:bcprov-jdk15on:1.56',
]

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()
    force dependencyVersions
  }
}

buildScan {
  licenseAgreementUrl = 'https://gradle.com/terms-of-service'
  licenseAgree = 'yes'
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.4.8'
  compile 'org.codehaus.groovy:groovy-json:2.4.8'

  compile 'org.slf4j:slf4j-api:1.7.22'
  testCompile 'ch.qos.logback:logback-classic:1.1.9'

  compile 'com.squareup.okio:okio:1.11.0'
  compile 'com.squareup.okhttp3:okhttp:3.6.0'
  testCompile 'com.squareup.okhttp3:mockwebserver:3.6.0'

  compile 'org.apache.commons:commons-compress:1.13'

  compile('com.kohlschutter.junixsocket:junixsocket-native-common:2.0.4') {
    exclude module: 'log4j'
  }
  compile('com.kohlschutter.junixsocket:junixsocket-common:2.0.4') {
    exclude module: 'log4j'
  }

  compile 'org.bouncycastle:bcpkix-jdk15on:1.56'

  testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude module: 'groovy-all'
  }
  testCompile 'cglib:cglib-nodep:3.2.4'

  testCompile 'commons-lang:commons-lang:2.6'
}

task sourcesJar(type: Jar) {
  dependsOn classes
  classifier 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

publishing {
  publications {
    dockerFilesocket(MavenPublication) {
      groupId 'de.gesellix'
      artifactId 'docker-filesocket'
      version artifactVersion
      from components.java
      artifact sourcesJar
    }
  }
}

bintray {
  user = System.env.BINTRAY_USER ?: project["bintray.user"]
  key = System.env.BINTRAY_API_KEY ?: project["bintray.key"]
  publications = ['dockerFilesocket']
  pkg {
    repo = 'docker-utils'
    name = 'docker-filesocket'
    desc = 'Unix Domain Sockets and Named Pipes for the JVM on Linux, macOS, and Windows'
    licenses = ['Apache-2.0']
    labels = ['docker', 'unix socket', 'linux', 'mac', 'named pipe', 'windows', 'engine api', 'remote api', 'client', 'java', 'groovy']
    version = [
        name: artifactVersion
    ]
    vcsUrl = 'https://github.com/docker-client/docker-filesocket.git'
  }
  dryRun = bintrayDryRun
}
bintrayUpload.dependsOn build

task wrapper(type: Wrapper) {
  gradleVersion = '3.2.1'
  distributionType = Wrapper.DistributionType.ALL
}
