import java.text.SimpleDateFormat

buildscript {
    ext {
        junit_runner_version = '1.0.0-RC3'
        kotlin_version = '1.1.4-2'
        spek_version = '1.1.4'
    }

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_runner_version"
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.8'
}

ext {
    artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
    bintrayDryRun = false
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'

def dependencyVersions = [
        "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
        "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
        'org.slf4j:slf4j-api:1.7.25',
]

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force dependencyVersions
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

repositories {
    mavenLocal()
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile 'io.github.microutils:kotlin-logging:1.4.6'
    compile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'

    compile 'com.squareup.okio:okio:1.13.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'

    compile('com.kohlschutter.junixsocket:junixsocket-native-common:2.0.4') {
        exclude module: 'log4j'
    }
    compile('com.kohlschutter.junixsocket:junixsocket-common:2.0.4') {
        exclude module: 'log4j'
    }

    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.spek:spek-api:$spek_version"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
//    testRuntime "org.junit.jupiter:junit-jupiter-api:5.0.0-M4"
//    testRuntime "org.junit.platform:junit-platform-runner:$junit_runner_version"
    testRuntime "org.junit.platform:junit-platform-launcher:$junit_runner_version"
//    testRuntime "org.junit.platform:junit-platform-console:$junit_runner_version"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

junitPlatform {
//    platformVersion junit_runner_version
    filters {
        engines {
            include 'spek'
        }
    }
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        dockerFilesocket(MavenPublication) {
            groupId 'de.gesellix'
            artifactId 'docker-filesocket'
            version artifactVersion
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER ?: project["bintray.user"]
    key = System.env.BINTRAY_API_KEY ?: project["bintray.key"]
    publications = ['dockerFilesocket']
    pkg {
        repo = 'docker-utils'
        name = 'docker-filesocket'
        desc = 'Unix Domain Sockets and Named Pipes for the JVM on Linux, macOS, and Windows'
        licenses = ['Apache-2.0']
        labels = ['docker', 'unix socket', 'linux', 'mac', 'named pipe', 'windows', 'engine api', 'remote api', 'client', 'java']
        version = [
                name: artifactVersion
        ]
        vcsUrl = 'https://github.com/docker-client/docker-filesocket.git'
    }
    dryRun = bintrayDryRun
}
bintrayUpload.dependsOn build

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionType = Wrapper.DistributionType.ALL
}
